Package sablecc.syntax;

// This grammar is strongly inspired from PlantUML's object and class diagram syntaxes.

Helpers

    letter = [['a'..'z'] + ['A'..'Z']];
    digit = ['0'..'9'];
    character = [0..0xFFFF];
    newline = 13 | 10 | 13 10;
    tab = 9;
    escape = '\';
    string_char = [character - [13 + 10]];
    not_escape_quote_string_char = [string_char - [escape + '"']];

Tokens

    start = '@startuml';
    end = '@enduml';
    title = 'title';
    conclusion_tag = 'conclusion';
    type = ('subconclusion' | 'strategy' | 'rationale' | 'domain' | 'support');
    link = ('-->' | '..>');
    separator = '-';
    assign = '=';
    string = '"' (not_escape_quote_string_char? (escape string_char)?)* '"';
    alias = letter (letter | digit)*;
    whitespace = (' ' | newline | tab)+;

Ignored Tokens
    whitespace;

Productions

diagram =
    start header? instruction* end;

header =
    {title} title string;

instruction =
    {declaration} declaration |
    {relation} relation;

declaration =
    {element} element |
    {conclusion} conclusion;

element =
    type alias assign [label]:string;

conclusion =
    {conclusion_without_restriction} conclusion_tag alias assign [label]:string |
    {conclusion_with_restriction} conclusion_tag alias assign [label]:string separator [restriction]:string;

relation =
    [left]:alias link [right]:alias;
